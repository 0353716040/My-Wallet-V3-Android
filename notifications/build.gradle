apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: '../quality/ktlint.gradle'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", 'true'
        }
        debug {
            buildConfigField "boolean", "USE_CRASHLYTICS", 'false'
        }
    }

    testOptions {
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
            }
            reports.junitXml.destination = file('../build/test-results/')
        }
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        abortOnError true
    }
}

dependencies {
    implementation project(':common:interface')
    implementation project(':core')

    api Libraries.firebaseCore
    api Libraries.firebaseMessaging
    implementation Libraries.firebaseDynamicLink
    implementation Libraries.firebaseCrashlytics
    implementation Libraries.firebaseAnalytics
    implementation (Libraries.firebaseConfig) {
        exclude group:'com.google.protobuf', module: 'protobuf-lite'
    }

    implementation Libraries.koinAndroid
    implementation Libraries.timber

    // Unit Test dependencies
    testImplementation project(':testutils')
    testImplementation project(':testutils-android')
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }
}
