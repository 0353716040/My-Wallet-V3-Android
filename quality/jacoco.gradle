apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'

task testEnvProdMinApi21DebugUnitTestCoverage(type: JacocoReport, dependsOn: "testEnvProdMinApi21DebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports on the Debug build."

    def excludedDirs = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/*$Lambda$*.*',
            '**/*Module.*',
            '**/*Dagger*.*',
            '**/*MembersInjector*.*',
            '**/*_Provide*Factory*.*',
            '**/Manifest*.*',
            '**/*Activity*.*',
            '**/*Dialog.*',
            '**/*Fragment*.*',
            '**/*ViewHolder*.*',
            '**/*Adapter*.*',
            '**/*zxing**',
            '**/*customviews**',
            '**/*databinding**',
            '**/injection**',
            '**/directory**',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/*_Factory.*',
            '**/*$*$*.*' // Anonymous classes generated by kotlin
    ]
    classDirectories = fileTree(
            dir: "${project.buildDir}/intermediates/classes/envProdMinApi21/debug",
            excludes: excludedDirs)

    def coverageSourceDirs = [
            "src/main/java"
    ]
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("${project.buildDir}/jacoco/testEnvProdMinApi21DebugUnitTest.exec")
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/testEnvProdMinApi21DebugUnitTestCoverage/testEnvProdMinApi21DebugUnitTestCoverage.xml"
}

tasks.coveralls {
    dependsOn 'testEnvProdMinApi21DebugUnitTestCoverage'
}