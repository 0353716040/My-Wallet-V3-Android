apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'

task testEnvProdDebugUnitTestCoverage(type: JacocoReport, dependsOn: "testEnvProdDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports on the Debug build."

    def excludedDirs = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/*$Lambda$*.*',
            '**/*Module.*',
            '**/*Dagger*.*',
            '**/*MembersInjector*.*',
            '**/*_Provide*Factory*.*',
            '**/Manifest*.*',
            '**/*Activity*.*',
            '**/*Dialog.*',
            '**/*Fragment*.*',
            '**/*ViewHolder*.*',
            '**/*Adapter*.*',
            '**/*zxing**',
            '**/*customviews**',
            '**/*databinding**',
            '**/injection**',
            '**/directory**'
    ]
    classDirectories = fileTree(
            dir: "${project.buildDir}/intermediates/classes/envProd/debug",
            excludes: excludedDirs)

    def coverageSourceDirs = [
            "src/main/java"
    ]
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("${project.buildDir}/jacoco/testEnvProdDebugUnitTest.exec")
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/testEnvProdDebugUnitTestCoverage/testEnvProdDebugUnitTestCoverage.xml"
}

tasks.coveralls {
    dependsOn 'testEnvProdDebugUnitTestCoverage'
}