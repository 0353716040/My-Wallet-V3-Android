import java.security.MessageDigest

/**
 * This is the checksum of the production google-services.json. If the config file gets updated,
 * please update this hash using {@code shasum -a 256 ~/google-services.json}, otherwise signing
 * release builds will fail.
 */
def checksum = '8367248d3a5a7ed4bf1dc197221753ede8f68cf9abd09e8f0c356dfa3b51c583'

task checkFirebaseConfig() {
    doFirst {
        File googleJson = new File("${project.rootDir}/app/google-services.json")
        if (calcSha256(googleJson) != checksum) {
            throw new GradleException("Production JSON file not found! Please double check that you're using the release google-services.json")
        }
    }
}

private static def calcSha256(file) {
    MessageDigest md = MessageDigest.getInstance("SHA-256");
    file.eachByte 4096, { bytes, size ->
        md.update(bytes, 0, size);
    }
    return md.digest().collect { String.format "%02x", it }.join();
}